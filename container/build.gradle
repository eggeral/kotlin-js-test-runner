apply plugin: 'kotlin2js'

def outDir = "${buildDir}/classes"

compileKotlin2Js{
    kotlinOptions.outputFile = "${outDir}/main/${project.name}_main.js"
    kotlinOptions.suppressWarnings = true
    kotlinOptions.version = true
    kotlinOptions.verbose = true
    kotlinOptions.sourceMap = true
}

compileTestKotlin2Js{
    kotlinOptions.outputFile = "${outDir}/test/${project.name}_test.js"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-js-library:$kotlin_version"
}

sourceSets {
    test.kotlin.srcDirs += "src/main/kotlin" // Needed for Gradle. Gradle Kotlin2JS does not include src/main/kotlin when building src/test/kotlin?
}


task copyJsFilesFromDependencies(type: Copy) {
    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    into "$outDir/main/lib"
    include { fileTreeElement ->
        def path = fileTreeElement.path
        path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
    }
}
build.dependsOn copyJsFilesFromDependencies
classes.dependsOn compileKotlin2Js